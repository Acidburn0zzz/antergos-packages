# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor:

pkgname=libcpuid
_pkgname=libcpuid
pkgver=0.2.0.r8.ca470c1
pkgrel=1
pkgdesc="Provides CPU identification for x86 (and x86_64)."
arch=('i686' 'x86_64')
url="https://github.com/eloaders/libcpuid"
license=('BSD-2-Clause')
makedepends=('git')
depends=('glibc')
conflicts=('libcpuid-git')
source=("git://github.com/eloaders/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  
  local v_ver=$(awk -F , '/^AC_INIT/ {gsub(/[\[\] -]/, ""); print $2}' configure.ac)
  printf "$v_ver.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}"/${_pkgname}
  
  autoreconf --install
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${_pkgname}
  
  make install DESTDIR="${pkgdir}"
  rm "${pkgdir}"/usr/lib/*.la
}
