# Maintainer: ZavonAJ <zavon at jazpr dot com>

pkgname=slingscold
pkgver=1.0.1
pkgrel=2
pkgdesc="A lighter launcher"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/slingscold/"
license=('GPL3')
depends=('libunique' 'libgee06' 'gnome-menus2' 'libwnck' 'gtk2' 'cairo')
makedepends=('cmake' 'vala')
source=("http://ppa.launchpad.net/noobslab/apps/ubuntu/pool/main/s/slingscold-launcher/slingscold-launcher_1.0.1.a.orig.tar.gz"
        '0001-Add-missing-lm-dep.patch')
sha256sums=('171e892f755808bd6a099eaad7e3301df5d30735bcbb0483d5557780c610e6f4'
         '6b1c97fe7863da5d7be000b3f139cc974ea86654c2b210be6eb1b1ac070a9a7c')

prepare() {
  cd "$srcdir"
  rm -rf build && mkdir build

  cd "$srcdir/slingscold-launcher_1.0.1"
  patch -p1 < "$srcdir"/0001-Add-missing-lm-dep.patch
}

build() {
  cd "$srcdir"/build
  cmake ../"slingscold-launcher_1.0.1" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
}

