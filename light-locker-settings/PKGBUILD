# Maintainer: Antergos Developers <dev AT antergos com>
# Contributor: Rob McCathie <korrode AT gmail>

pkgname=light-locker-settings
pkgver=1.5.0
pkgrel=2
pkgdesc="Just a simple settings dialog for light-locker"
arch=('any')
url="https://launchpad.net/${pkgname}"
license=('GPL3')
depends=('light-locker' 'python-gobject' 'python-psutil')
makedepends=('intltool')
source=("${url}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2"
        "bugfix.patch")
sha1sums=('87a1a0f34c5bd9f53b93d0a5f30153c1d5a34d69'
          '768641a1a50817f1664e148c0ddb4fc3d205570a')

prepare() {
    cd $srcdir/$pkgname-$pkgver
    patch -p1 -i $srcdir/bugfix.patch
}


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
