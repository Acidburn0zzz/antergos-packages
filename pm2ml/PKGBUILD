#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pm2ml
pkgver=2013.4
pkgrel=1
pkgdesc='Generate metalinks for downloading Pacman packages and databases.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pm2ml"
depends=(python3 pyalpm)
optdepends=('python3-aur: AUR support' 'aria2: ppl script support.' 'reflector: Reflector support')
backup=(etc/ppl.conf)
source=(
  http://xyne.archlinux.ca/projects/pm2ml/src/pm2ml-2013.4.tar.xz
  http://xyne.archlinux.ca/projects/pm2ml/src/pm2ml-2013.4.tar.xz.sig
)
sha512sums=(
  0d9a834a3a9e4487c985e0d6a49c22542ea1c4a1275a6207ac09ecf8272926908a72f81de1aa50c506b16f26ce2ae94bcc6f19fd6deb5e5c82a9046db76a941d
  cc3aba1bb797eb9c3dd9fca534e7c64dc0125454b60e5e17c5fd6eba582ba864af33badaa2a2389d026ffd6a39611cb439dbdcdc9e99b9306e5126529792a6c8
)
md5sums=(
  d88e7728000c9f2a18460344e3fefa08
  96a7896be0c7f23e410d5e57c5d5ddf9
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir"/ --optimize=1
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  for foo_ in ppl pplsyu ppls; do
    install -Dm755 "$foo_" "$pkgdir/usr/bin/$foo_"
  done
  install -Dm644 "ppl.conf" "$pkgdir/etc/ppl.conf"
}

# vim: set ts=2 sw=2 et:
