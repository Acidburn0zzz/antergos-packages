# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>

_pkgname=mdm
pkgname=mdm-display-manager
pkgver=1.1.3
pkgrel=1
pkgdesc="MDM Display Manager for managing local and remote displays"
url="https://github.com/linuxmint/mdm"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('libdmx' 'libgnomecanvas' 'librsvg' 'gksu' 'webkitgtk2'\
         'python2' 'hicolor-icon-theme' )
makedepends=('pkgconfig' 'intltool' 'xtrans' 'xorg-server' 'gnome-doc-utils')
conflicts=('gdm')
options=('!libtool')
install=${pkgname}.install
backup=('etc/pam.d/mdm' 'etc/pam.d/mdm-autologin' 'etc/mdm/custom.conf')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/mdm/archive/1.1.3.tar.gz"
        "mdm.pam"
        "mdm-autologin.pam"
        "mdm.service"
        "org.cinnamon.pkexec.mdmsetup.policy"
        "gnome-autogen.sh")
sha256sums=('76ba9906ac837a67260f96a1824ce7e387c78d7ef469b3ceb8f93c2cc2bfefb0'
            'efcd8bc6e50dcaac371d3da55172d79ae01b67538eb078b200bb83ea498d662f'
            '6a8b286d1ffa04150b3cc401f64e6ddec778c7b65f5bfc831031b64345d7e6b2'
            'c2fe4bee7755d9deea39dbb1b3e260d0512edfdf504c597e152ca33d558a1dc0'
            'c2cbe920d801a978510b8c15066742bd9ff6916b6d4944efa0269a3022ae19af'
            '9ffc7f44f5ba5ffea6964614daf66e049ad1f68488ce34bc6e1565b799659d77')

build() {
    cd ${_pkgname}-${pkgver}

    cp ${srcdir}/gnome-autogen.sh .
    sed -i 's/gnome-autogen.sh/.\/gnome-autogen.sh/g' autogen.sh
    chmod +x gnome-autogen.sh
    sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.ac

    ./autogen.sh --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/mdm \
        --localstatedir=/var \
        --disable-static \
        --disable-scrollkeeper \
        --without-tcp-wrappers \
        --without-console-kit \
        --enable-ipv6=yes \
        --with-xevie=yes \
        --enable-secureremote=yes

    LDFLAGS="-lXau -lm"
    sed -i -e 's|${prefix}|/usr|' config.h

    make
}

package() {
    cd $_pkgname-$pkgver

    make DESTDIR="${pkgdir}" install

    install -m644 "${srcdir}/mdm.pam" "${pkgdir}/etc/pam.d/mdm"
    install -m644 "${srcdir}/mdm-autologin.pam" "${pkgdir}/etc/pam.d/mdm-autologin"
    install -D -m644 ${srcdir}/mdm.service ${pkgdir}/usr/lib/systemd/system/mdm.service

    #PolicyKit execution scheme
    install -m755 -d ${pkgdir}/usr/share/polkit-1/actions/
    install -m644 ${srcdir}/org.cinnamon.pkexec.mdmsetup.policy $pkgdir/usr/share/polkit-1/actions/

    sed -i -e 's|^Exec=gksu|Exec=pkexec|' $pkgdir/usr/share/mdm/applications/mdmsetup.desktop
    sed -i -e 's|^NoDisplay=true|NoDisplay=false |' $pkgdir/usr/share/mdm/applications/mdmphotosetup.desktop

    chmod 0750 ${pkgdir}/usr/lib/mdm

    find "${pkgdir}" -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
    rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop
    rmdir $pkgdir/etc/dm
}
