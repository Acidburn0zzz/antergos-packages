#Maintainer: Alex Filgueira <alexfilgueira@antergos.com>

_pkgname=Cnchi
pkgname=cnchi
pkgver=0.6.20
pkgrel=1
pkgdesc='Graphical Installer for Antergos Linux'
arch=(any)
license=(GPL)
url="https://github.com/Antergos/Cnchi"
depends=(python pyalpm python-gobject python-dbus hdparm python-cairo libtimezonemap
         pyparted hwinfo webkitgtk upower python-mako iso-codes)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/Antergos/Cnchi/archive/v${pkgver}.tar.gz")
sha256sums=('6ed81036ee6369b505042ffaa8a790ba19c0bb493aa3068a0e55385d9db8f643')

build ()
{
  cd "$srcdir/$_pkgname-$pkgver"
}

package() {
	cd ${srcdir}/${_pkgname}-$pkgver

	install -d ${pkgdir}/usr/share/locale/
	install -d ${pkgdir}/usr/share/cnchi
	install -Dm755 "${pkgname}.py" "$pkgdir/usr/share/cnchi/${pkgname}.py"
	install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/cnchi"
	install -Dm755 "scripts/testing.sh" "${pkgdir}/usr/bin/cnchi-dev"
	install -Dm755 "update.info" "$pkgdir/usr/share/cnchi/update.info"

	for i in data scripts src ui; do
		cp -R ${i} "${pkgdir}/usr/share/cnchi/"
	done

  	for files in po/*; do
    	if [ -f "$files" ] && [ "$files" != 'po/cnchi.pot' ]; then
        	STRING_PO=`echo ${files#*/}`
        	STRING=`echo ${STRING_PO%.po}`
        	mkdir -p ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES
        	msgfmt $files -o ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES/cnchi.mo
        	echo "${STRING} installed..."
    	fi
	done
}
