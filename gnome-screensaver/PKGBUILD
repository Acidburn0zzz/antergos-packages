# $Id: PKGBUILD 169908 2012-10-30 22:34:33Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor (ubuntu patches): Alex Filgueira <alexfilgueira@cinnarch.com>

pkgname=gnome-screensaver
epoch=1
pkgver=3.6.1
pkgrel=2
pkgdesc="Screensaver designed to integrate well with the GNOME desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GnomeScreensaver"
backup=(etc/pam.d/gnome-screensaver)
depends=('dbus-glib' 'libgnomekbd' 'gnome-desktop' 'gsettings-desktop-schemas' 'libsystemd')
makedepends=('pkgconfig' 'intltool' 'libxss')
groups=('gnome')
options=(!emptydirs)
install=gnome-screensaver.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
       gnome-screensaver.pam
       series
       03_fix_ltsp-fading.patch
       04_force_blank_only_on_ltsp.patch
       05_dbus_service.patch
       10_legacy_scrsvr_inhibit.patch
       11_autostart_delay.patch
       13_nvidia_gamma_fade_fallback.patch
       14_no_fade_on_user_switch.patch
       15_dont_crash_on_no_fade.patch
       16_dont_crash_in_kvm.patch
       17_remove_top_panel.patch
       18_unity_dialog_layout.patch
       19_autostart_in_unity.patch
       24_use_user_settings.patch
       25_fix_lock_command.patch
       26_lightdm_greeter_on_lock.patch
       27_lightdm_switch_user.patch
       28_blocking_return.patch
       29_handle_expired_creds.patch)

sha256sums=('f39b78d4f7fed748c7f0a31d694112fb907c6d3c4e63db22eb858df07e962cd0'
            'c19870c4f2d6e91ba0eea2603753a74e0c5c82ff5fa3cacb1198027a38d0a6cb'
            '664319550bbed15284b09d426b9ff2cb730b69b72e22216b46dd69114711aa0b'
            '92dc68f6bc19852e54ed929bd84eef9d0188a78fe47439316126b9c0c9f5c485'
            '9cee6882799dfcd58c136a35a8f5ec71dc6e04934b142fd04477a1043fb1841f'
            '28eb83c275b59fcebd7794a9c33c5c8b21a516c156b0c93f2f01e28dcb9ddf40'
            'dc3ba1108575b2c33d50f9725d6696ed34041b8458f07de379fb51f97da74e10'
            '965a00985b35675fd04c3d67aee68e757bd6a4c067f001854833e8be293097e4'
            '7906efd34a30a42e6a252d8b859155e40fd6037111a6e5c3004c71e70b581e6f'
            '37c70c2ea7f7ff575ba1dde6a88d020299e6984e81bb0b85ff01637a0ecd816a'
            'cfdc26eea9009c88b923e71ed847b412b628d25276a8edf0639e341ed0813575'
            '8adb6c5adb131ffbe65b39d9dc0e7a5bac895b9368a353bc554bdf9c8528925b'
            '1449aa865fc33f682128a7ab1a2282a03a51356f247caf9d7a99bfd9fe59a084'
            '682025caaf66cda6044b7cc0260122c8ecafa88cbbc2f6cc320237343e814e50'
            '164534d33ad1ea639495d173d2cc976399549055d0bcac8cc5f55ebb76e2cc86'
            'fb0a42b7e03d62853ab9efdadcc9c4832bcb0177b9820914f05d60f05c8094c5'
            '95624a0c8e0ab0222302e3ebd6d2b63f59f5163bf2e99bd6dca460da2e846cee'
            '9effc109fa2bf03bfdda8f8ae5e1488892fba36ce47401f7aec2709bde76259c'
            '5d40bd6e1843150912dc52932eaa94c0f6d57931fd45a3769ef40e2335fe3c6f'
            '893decfa1f3d8b9148d44ba5e85ee15e5f97b0cee96e9126d1b3aeaf7686c16e'
            '1d8dcb1d70a9106e8cbfe36a696e6a85c5bc0e1347fd25ffdbf92f93ce16ee71')

build() {
  cd $pkgname-$pkgver


  for i in $(grep -v '#' "${srcdir}/series"); do
    patch -Np1 -i "${srcdir}/${i}"
  done
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-screensaver \
      --localstatedir=/var \
      --with-mit-ext --with-systemd

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../gnome-screensaver.pam "$pkgdir/etc/pam.d/gnome-screensaver"
}
