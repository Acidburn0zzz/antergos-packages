# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=hotot-gtk3-git
pkgver=20130205
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hotot-data-git' 'python2-gobject'
         'webkitgtk3' 'desktop-file-utils' 'python2-dbus')
makedepends=('git' 'cmake' 'intltool' 'python2')
optdepends=('libappindicator: unity menubar integration')
provides=("${pkgname/-git}")
conflicts=("${pkgname/-git}")
install="$pkgname.install"

_gitroot="git://github.com/lyricat/Hotot.git"
_gitname="hotot"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DWITH_GTK=ON \
          -DWITH_GIR=ON \
          -DWITH_QT=OFF \
          -DWITH_KDE=OFF \
          -DWITH_CHROME=OFF \
          -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package_hotot-gtk3-git() {
  cd "$srcdir/$_gitname-build"

  # bin
  install -Dm755 scripts/hotot \
    "$pkgdir/usr/bin/hotot-gtk3"
  sed -i "s/hotot/&_gir/" "$pkgdir/usr/bin/hotot-gtk3"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/hotot_gir"
  install -Dm644 hotot-gir/*.py* \
    "$pkgdir/usr/lib/python2.7/site-packages/hotot_gir"

  # desktop file
  install -Dm644 misc/hotot.desktop \
    "$pkgdir/usr/share/applications/hotot-gtk3.desktop"
  sed -i "/Exec/s/hotot/&-gtk3/;/^Name/s/$/ Gtk3/;s/2/3/" \
    "$pkgdir/usr/share/applications/hotot-gtk3.desktop"
}

# vim:set ts=2 sw=2 et:
